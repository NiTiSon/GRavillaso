import java.awt.Desktop

buildscript {
    repositories{
        mavenCentral()
        maven{ url 'https://www.jitpack.io' }
    }
    dependencies {
        classpath "com.github.Anuken.Arc:arc-core:v$mindustryVersion"
        classpath "com.github.Anuken.Mindustry:core:v$mindustryVersion"
        classpath "com.github.Anuken.Mindustry:tools:v$mindustryVersion"
        classpath "com.github.Anuken.Arc:packer:v$mindustryVersion"
    }
}

plugins {
    id "java"
    id "maven-publish"
}

sourceSets.main{
    java.srcDir("src/")
    resources.srcDir("assets/")
}

repositories{
    mavenCentral()
    maven{url "https://jitpack.io"}
}

dependencies{
    annotationProcessor "com.github.Anuken:jabel:$jabelVersion"
    compileClasspath "com.github.Anuken.Mindustry:core:$mindustryVersion"
    implementation  "com.github.Zelaux.ZelauxModCore:core:$zelauxCoreVersion"
}

ext{
    sdkRoot = System.getenv("ANDROID_HOME")
    sdkVersion = '30'

    doExec = { cmd ->
        def proc = cmd.execute(null, new File("$buildDir/libs"))
        proc.waitForProcessOutput(System.out, System.err)
    }
}


def isWindows = System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("windows");

jar.archiveFileName.set("${project.name}-Desktop.jar")

jar {
    dependsOn "core:jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE



    from {
        project(":core").fileTree("build/libs/core.jar").collect { it.isDirectory() ? it : zipTree(it) }
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from(rootDir) {
        include "mod.hjson"
        include "mod.json"
        include "icon.png"
    }

    from("core/assets/") {
        include "**"
    }
}

task compile(type: Jar) {
    dependsOn "tools:pack"
    dependsOn "core:jar"

    archiveFileName.set("${project.name}-Desktop.jar")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        project(":core").fileTree("build/libs/core.jar").collect { it.isDirectory() ? it : zipTree(it) }
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from(rootDir) {
        include "mod.hjson"
        include "mod.json"
        include "icon.png"
    }

    from("core/assets/") {
        include "**"
    }
}

task dexify(type: Jar){
    archiveFileName.set("${project.name}.jar")

    final File jarArtifact = new File(tasks.jar.archiveFile.get().asFile.parent, "${project.name}-Desktop.jar"),
               dexedArtifact = new File(tasks.dexify.getTemporaryDir(), "dexed.jar")


    doFirst{
        def files = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File("$sdkRoot/platforms/android-$sdkVersion/android.jar")])

        exec{
            workingDir dexedArtifact.parent
            def command = ["d8", "--min-api", "14"]
            for(def file : files){
                command += "--classpath"
                command += file.path
            }

            command += ["--output", dexedArtifact, jarArtifact]

            standardOutput(System.out)

            if(isWindows){
                commandLine("cmd", "/c", *command)
            }else{
                commandLine(*command)
            }
        }
    }

    from(zipTree(jarArtifact), zipTree(dexedArtifact))
}

task buildDex dependsOn "build", "dexify"
task compileDex dependsOn "compile", "dexify"

task compileLaunch {
    dependsOn "compile"
    doLast {
        copy {
            from "$buildDir/libs/${project.name}-Desktop.jar"
            into System.getenv("MINDUSTRY_MODS")
        }

        Desktop.desktop.browse "steam://rungameid/1127400".toURI()
    }
}
allprojects {
    apply plugin: "java"

    repositories {
        mavenCentral()
        maven{ url 'https://www.jitpack.io' }
    }

    tasks.withType(JavaCompile) {
        targetCompatibility = 15
        sourceCompatibility = JavaVersion.VERSION_16
        options.compilerArgs.addAll(['--release','15'])
    }
    java {
        withJavadocJar()
        withSourcesJar()
    }
}
project(":tools") {
    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])

        implementation project(":core")

        implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.22.1'
        implementation "com.github.Anuken.Mindustry:core:$mindustryVersion"
        implementation "com.github.Zelaux.ZelauxModCore:tools:$zelauxCoreVersion"
        implementation "com.github.Zelaux.ZelauxModCore:core:$zelauxCoreVersion"
    }
}
project(":core") {
    apply plugin: "java"

    compileJava.options.fork = true

    task preGen {
        outputs.upToDateWhen { false }
    }
    task sourceJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    task assetsJar(type: Jar, dependsOn: ":tools:pack") {
        archiveClassifier = 'assets'
        from files("assets") {
            exclude "config", "cache", "music", "sounds"
        }
    }

    artifacts {
        archives sourceJar
        archives assetsJar
    }

    dependencies {
        compileClasspath "com.github.Anuken.Arc:arc-core:$mindustryVersion"
        compileClasspath "com.github.Anuken.Mindustry:core:$mindustryVersion"
        implementation "com.github.Zelaux.ZelauxModCore:core:$zelauxCoreVersion"
        annotationProcessor "com.github.Anuken:jabel:$jabelVersion"
    }

    afterEvaluate {
        task mergedJavadoc(type: Javadoc) {
            if (true) return null;
            def blacklist = [project(":annotations")]
            if (findProject(":android") != null) {
                blacklist += project(":android")
            }

            source rootProject.subprojects.collect { project ->
                if (!blacklist.contains(project) && project.hasProperty("sourceSets")) {
                    return project.sourceSets.main.allJava
                }
            }

            classpath = files(rootProject.subprojects.collect { project ->
                if (!blacklist.contains(project) && project.hasProperty("sourceSets")) {
                    return project.sourceSets.main.compileClasspath
                }
            })
            destinationDir = new File(buildDir, 'javadoc')
        }
    }
}